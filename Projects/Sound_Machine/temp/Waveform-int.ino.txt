// Display analog input to mini-OLED I2C,
// capture input inside TIMER1 ISR
// http://arduino-er.blogspot.com/

// OLED display: ref u8glib: https://code.google.com/p/u8glib/
// To install u8glib on Arduino IDE: http://goo.gl/j3olBA
#include "U8glib.h"

U8GLIB_SSD1306_128X64 u8g(U8G_I2C_OPT_NONE|U8G_I2C_OPT_DEV_0);

const int LCD_WIDTH = 128;
const int LCD_HEIGHT = 64;

//true: request capture analog input in ISR
//false: stop capture, draw waveform in loop
boolean capture = false;

int LCD_X;
int LCD_Y[LCD_WIDTH];

/*
    reference: Arduino Timer and Interrupt Tutorial
    http://blog.oscarliang.net/arduino-timer-and-interrupt-tutorial/
    
    For our TIMER1 Interrupt:
    Clock Freq = 16MHz
    no prescale, 1
    16MHz - 0.0625us/cycle
   
    To calculator preload value to generate 1ms(1KHz) 
    (65536 - t) x 0.0625us = 1000us
    t = 65536 - 1000/0.0625 = 49536
    
    To calculator preload value to generate 0.5ms(2KHz)
    (65536 - t) x 0.0625us = 500us
    t = 65536 - 500/0.0625 = 57536

 */
const int TCNT1_PRELOAD = 57536;

void clearY(){
  for(int i=0; i<LCD_WIDTH; i++){
    LCD_Y[i] = -1;
  }
}

void drawY(){
  u8g.drawPixel(0, LCD_Y[0]);
  for(int i=1; i<LCD_WIDTH; i++){
    u8g.drawLine(i-1, LCD_Y[i-1], i, LCD_Y[i]);
  }
}

void LCDsetup(void) {
  // initialize Timer1
  noInterrupts(); // disable all interrupts
  TCCR1A = 0;
  TCCR1B = 0;
  TCNT1 = TCNT1_PRELOAD;
  TCCR1B |= (1 << CS10);   // no prescaler
  TIMSK1 |= (1 << TOIE1); // enable timer overflow interrupt
    
  LCD_X = 0;
  clearY();
  capture = true;

  interrupts(); // enable all interrupts  
}

void LCDloop(void) {
  if(!capture){
    
    u8g.firstPage();  
    do {
      drawY();
    } while( u8g.nextPage() );
    
    //start capture another frame
    LCD_X = 0;
    clearY();
    capture = true;
  }

  delay(1);

}

ISR(TIMER1_OVF_vect){
  TCNT1 = TCNT1_PRELOAD; // preload timer
  
  if(capture){  
    LCD_Y[LCD_X] = map(getSma(), 0, 1023, LCD_HEIGHT-1, 0);
    
    LCD_X++;
    if(LCD_X >= LCD_WIDTH){
      capture = false;
    }
  }
}
